#+TITLE: Example Definitions Finite Groups - Mizar
#+AUTHOR: Alex Nelson
#+EMAIL: pqnelson@gmail.com
#+DATE: <2022-02-19T19:09:20-08:00>
#+LANGUAGE: en
#+OPTIONS: H:5
#+HTML_DOCTYPE: html5
#+INCLUDE: ../../../org-macros.org
#+HTML_LINK_UP: ./index.html
#+HTML_LINK_HOME: ../../../index.html
#+HTML_HEAD_EXTRA: <link rel="stylesheet" type="text/css" href="../../../css/stylesheet.css" />
# Created Saturday February 19, 2022 at  7:09PM

* Introduction
:PROPERTIES:
:CUSTOM_ID: h-3514a396-c340-41d5-97f5-7fe965d4519c
:END:

This is a collection of definitions I intend to formalize in Mizar. The
hard part of the formalizations is the proof of existence and uniqueness
(or other correctness conditions).

* Extra-special groups
:PROPERTIES:
:CUSTOM_ID: h-2f274d71-e96e-4479-8d58-cfc921adb1df
:END:

Rotman's /Introduction to the Theory of Groups/, 4th ed, defines on page
124: 

#+begin_definition
Let $G$ be a finite $p$ -group. We call it {{{dfn(Extra-special)}}}
if $Z(G)$ is cyclic and $\Phi(G)=Z(G)=G'$.
#+end_definition

This defines an attribute (hinted by being of the form "We call it ~<hairy>~
if..."). Its formalization is straightforward.

#+begin_src mizar
definition
  let p be prime Nat;
  let IT be finite p-group Group;
  attr IT is extra-special means
  (center IT is cyclic) & (Phi(IT) = center IT) &
  (center IT = IT`);
end;
#+end_src

* G Has Characteristic p
:PROPERTIES:
:CUSTOM_ID: h-2e5a9e76-d90b-4603-ac16-2df78d4fc0a8
:END:

These definitions are taken from {{{arXiv(1906.07216)}}}.

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group whose order is
divisible by $p$. A {{{dfn($p$-local subgroup)}}} is one that looks like
$N_{H}(P)$ where $P$ is a nontrivial $p$-subgroup of $G$.
#+end_definition

My intuition is that this definition introduces an attribute "$p$-local"
for subgroups.

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group such that p divides card G;
  let IT be Subgroup of G;
  attr IT is p-local means
  :Def:
  ex P being non trivial p-group Subgroup of G
  st the multMagma of IT = Normalizer P;
end;
#+end_src

Since subgroup equality is defined only for strict subgroups, we cannot
say ~IT = Normalizer P~ without making ~IT~ a strict Subgroup of G. (The
normalizer of a subgroup is defined to be a strict subgroup.) For this
reason, we needed to check ~the multMagma of IT = Normalizer P~.

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group, $L\leq G$ be a subgroup.
We say $L$ {{{dfn(has Characteristic $p$)}}} if
$C_{G}(O_{p}(L))\leq O_{p}(L)$.
#+end_definition

This introduces a new predicate, ~has_characteristic p~ for a subgroup.

#+begin_src mizar
definition
  let p be prime Nat;
  let G be Group;
  let L be Subgroup of G;
  pred L has_characteristic p means
  :Def:
  Centralizer (p-core L) is Subgroup of (p-core L);
end;
#+end_src

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group.
We say $G$ {{{dfn(has local Characteristic $p$)}}} if
if for all $p$-local subgroups $L$ of $G$ has characteristic $p$.
#+end_definition

(A special case when $p = 2$, we just say that $G$ has even
Characteristic.)

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  pred G has_local_characteristic p means
  :Def:
  for L being Subgroup of G st L is p-local holds L has_characteristic p;
end;
#+end_src

From page ix of [[https://users.math.msu.edu/users/meierfra/Preprints/CGP/Structure/structure.pdf][The Local Structure Theorem For Finite Groups With A Large p-Subgroup]]:

#+begin_definition
A finite group $G$ has {{{dfn(characteristic $p$)}}} if $C_{G}(O_{p}(G))\leq O_{p}(G)$.
#+end_definition

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  pred G has_characteristic p means
  :Def:
  Centralizer (p-core G) is Subgroup of (p-core G);
end;
#+end_src

* Parabolic Subgroup
:PROPERTIES:
:CUSTOM_ID: h-03074538-22a6-4bac-869b-a27c2bd4004c
:END:

#+begin_definition
Let $G$ be a finite group.
Any subgroup of $G$ containing a Sylow p-subgroup of $G$ is a
{{{dfn(parabolic subgroup)}}} of $G$.
#+end_definition

This defines an attribute "parabolic" for "Subgroup of $G$". We need to
use the Mizar predicate [[http://mizar.org/version/current/html/group_10.html#R3][~is_Sylow_p-subgroup_of_prime~]] to do the heavy lifting:

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  let IT be Subgroup of G;
  attr IT is parabolic means
  :Def:
  ex P being Subgroup of G st P is_Sylow_p-subgroup_of_prime p &
  P is Subgroup of IT;
end;
#+end_src

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group.
We say $G$ has {{{dfn(parabolic characteristic $p$)}}} if
every $p$-local parabolic subgroup of $G$ has characteristic $p$.
#+end_definition

This defines a predicate (whenever a gadget "has ~<property>~", it
defines a predicate).

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  pred G has_parabolic_characteristic p means
  :Def:
  for H being parabolic Subgroup of G st H is p-local
  holds H has_characteristic p;
end;
#+end_src
* Finite Group Theory
:PROPERTIES:
:CUSTOM_ID: h-5ae83f58-ed1c-4543-8e5d-a49c61ba6701
:END:

** Sections of a Group
:PROPERTIES:
:CUSTOM_ID: h-86c68c5f-b76f-407c-aab0-aa4aecc6ceb9
:END:

Kurzweill and Stellmacher's /Theory of Finite Groups/ \S 1.3 concludes
with a brief definition in passing:

#+begin_definition
Let $G$ be a group, $N\trianglelefteq H\leq G$. We call the quotient
$H/N$ a {{{dfn(Section)}}} of $G$.
#+end_definition

In particular, $N$ is a normal subgroup of $H$ (viz., $N$ is not
necessarily a normal subgroup of $G$).

This appears to define a new mode (as hinted by "~<bork>~ of $G$").
*However* not all groups have a section (the quaternions famously do
not).

Consequently, we need to define this as a predicate.

#+begin_src mizar
definition
  let K,G be Group;
  pred K is_section_of G means :Def:
  ex H being Subgroup of G st
  ex N being normal Subgroup of N st
  K = H./.N;
end;
#+end_src

We also have a related notion "$G$ is involved in $K$" means $G$ is
isomorphic to a section of $K$. This is a transitive relation.

* Subgroup Series
:PROPERTIES:
:CUSTOM_ID: h-e5583d34-8606-4622-bc9a-3b915cb5eb4a
:END:

I will review the various subgroup series, and --- if they've been
already defined (implicitly or explicitly) --- their implementation.

** Central Series
:PROPERTIES:
:CUSTOM_ID: h-4ecb1fc1-3d39-4555-8a61-3cf134915242
:END:

The definition of a nilpotent group uses a central series
$G=Z_{1}\geq Z_{2}\geq\dots\geq Z_{n}=\mathbf{1}$ where
each $Z_{k}\trianglelefteq G$ and the quotient satisfies
$Z_{k}/Z_{k+1}\leq Z(G/Z_{k+1})$.

#+begin_src mizar
definition
  let IT be Group;
  attr IT is nilpotent means :Def2: :: GRNILP_1:def 2
  ex F being FinSequence of the_normal_subgroups_of IT st
  (len F > 0 & F.1 = (Omega).IT & F.(len F) = (1).IT &
   (for i being Element of NAT st i in dom F & i+1 in dom F
    for G1, G2 being strict normal Subgroup of IT
    st G1 = F.i & G2 = F.(i + 1)
    holds (G2 is Subgroup of G1 &
           G1./.((G1,G2)`*`) is Subgroup of
             center (IT./.G2))));
end; 
#+end_src

** Subnormal Series
:PROPERTIES:
:CUSTOM_ID: h-193c557d-a853-468a-a56f-ba3d2dfcf3ba
:END:

A solvable group is defined by having a subnormal series
$G=N_{1}\trianglerighteq N_{2}\trianglerighteq\dots\trianglerighteq N_{n}=\mathbf{1}$
--- that is, $N_{j+1}\trianglelefteq N_{j}$ for $j=0,\dots,n-1$ ---
whose factor groups $N_{k}/N_{k+1}$ are Abelian.

#+begin_src mizar
definition
  let IT be Group;
  attr IT is solvable means :Def1: :: GRSOLV_1:def 1
  ex F being FinSequence of Subgroups IT st
  (len F > 0 & F.1 = (Omega).IT & F.(len F) = (1).IT &
   (for i being Element of NAT st i in dom F & i+1 in dom F
    for G1, G2 being strict Subgroup of IT
    st G1 = F.i & G2 = F.(i+1)
    holds (G2 is strict normal Subgroup of G1 &
           (for N being normal Subgroup of G1 st N = G2
            holds G1 ./. N is commutative))));
end; 
#+end_src

** Composition Series
:PROPERTIES:
:CUSTOM_ID: h-f057b7e7-9fa8-4236-9410-dfac6ff1e4df
:END:

A famous example of a subgroup series is the composition
series for a group. Mizar defines it as a subgroup series
$\mathbf{1}=A_{n}\trianglelefteq\dots\trianglelefteq A_{2}\trianglelefteq A_{1}=G$.
Although this seems to be using a strange convention, Mizar is following
Bourbaki's /Algbra/. The more familiar notion of a composition
series (as a ``maximal'' subnormal series) requires the
~jordan_holder~ attribute (again, this follows Bourbaki's
/Algebra/ Definition 10 in I.4.7).

#+begin_src mizar
:: ALG I.4.7 Definition 9
definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be FinSequence of the_stable_subgroups_of G;
  attr IT is composition_series means
  :: GROUP_9:def 28
  IT.1=(Omega).G & IT.(len IT)= (1).G &
  for i being Nat st i in dom IT & i+1 in dom IT
  for H1,H2 being StableSubgroup of G
  st H1=IT.i & H2=IT.(i+1)
  holds H2 is normal StableSubgroup of H1;
end;
#+end_src

** Lower Central Series
:PROPERTIES:
:CUSTOM_ID: h-7b9e0126-cb6c-4b44-8ae6-245ce59a4467
:END:

The lower central series may be defined as
$G=A_{1}\trianglerighteq A_{2}\trianglerighteq\dots\trianglerighteq A_{n}=\mathbf{1}$,
where $A_{k+1}=[A_{k},G]=[G,A_{k}]$.
Mizar implicitly defines this in ~GRNILP1:Th27~

#+begin_src mizar
theorem :: GRNILP_1:27
  for G being Group
  st ex F being FinSequence of the_normal_subgroups_of G
     st len F > 0 & F.1 = (Omega).G & F.(len F) = (1).G &
     for i st i in dom F & i+1 in dom F
     for G1 being strict normal Subgroup of G st G1 = F.i
     holds [.G1, (Omega).G.] = F.(i+1)
  holds G is nilpotent
proof end;
#+end_src

** Upper Central Series
:PROPERTIES:
:CUSTOM_ID: h-a6f950fd-41bb-4c6c-9e3f-2c3828fdec89
:END:

This is a subgroup series
$Z^0(G) \leq Z^1(G) \leq Z^2(G) \leq \dots$,
where $Z^{0}(G)=\mathbf{1}$,
$Z^1(G) = Z(G)$,
$Z^2(G)/Z^1(G) = Z(G/Z^1(G))$,
$Z^3(G)/Z^2(G) = Z(G/Z^2(G))$, and so on. Each $Z^{n}(G)$ is a normal
subgroup of $G$.

In general, $Z^{n+1}(G)/Z^{n}(G) = Z(G/Z^{n}(G))$ defines the $(n+1)$
term in the series. Explicitly, we would have

$$ Z^{n+1}(G) = \{x\in G\mid \forall y\in G, [x,y]\in Z^{n}(G)\}. $$

We could thus propose the definition (based on modifying the definition
~GRNILP_1:def 2~ to use ~NAT_1~ for ~mode sequence of X is Function of NAT,X~):

#+begin_src mizar
definition
  let G be Group;
  func UpperCentralSeries G -> sequence of the_normal_subgroups_of G means
  :Def:
  (it.0 = (1).G &
   (for i being Element of NAT st i in dom it & i+1 in dom it
    for G1, G2 being strict normal Subgroup of G
    st G1 = it.i & G2 = it.(i + 1)
    holds (G1 is Subgroup of G2 &
           G2./.((G2,G1)`*`) = center (G./.G1))));
  existence
  proof
    thus ex Z being sequence of the_normal_subgroups_of G st
     (Z.0 = (1).G &
      (for i being Element of NAT st i in dom Z & i+1 in dom Z
       for G1, G2 being strict normal Subgroup of G
       st G1 = Z.i & G2 = Z.(i + 1)
       holds (G1 is Subgroup of G2 &
              G2./.((G2,G1)`*`) = center (G./.G1))));
  end;
  uniqueness
  proof
    defpred P[sequence of the_normal_subgroups_of G] means
    ($1.0 = (1).G &
      (for i being Element of NAT st i in dom $1 & i+1 in dom $1
       for G1, G2 being strict normal Subgroup of G
       st G1 = $1.i & G2 = $1.(i + 1)
       holds (G1 is Subgroup of G2 &
              G2./.((G2,G1)`*`) = center (G./.G1))));
    for Z1,Z2 being sequence of the_normal_subgroups_of G st
    P[Z1] & P[Z2] holds Z1=Z2;
    hence thesis;
  end;
end;
#+end_src

We would then have to prove

#+begin_src mizar
theorem
  for G be Group
  for n be Nat
  holds the carrier of (UpperCentralSeries G).(n+1) =
        {g where g is Element of G :
         for y being Element of G
         holds [.g,y.] in (UpperCentralSeries G).n};
#+end_src

* Matrix Groups
:PROPERTIES:
:CUSTOM_ID: h-03dcc477-8208-48d3-92fb-d3284b5e561a
:END:

** General Linear Group
:PROPERTIES:
:CUSTOM_ID: h-edc440ad-dc16-46df-9865-b20fe366520d
:END:

#+begin_definition
Let $K$ be a field, $n\in\NN$ be a positive integer.
The {{{dfn(General Linear Group)}}} denoted $GL(n,K)$ or $GL_{n}(K)$ is
the group consisting of invertible $n\times n$ matrices over the field
$K$ using matrix multiplication as the binary operation.
#+end_definition

#+begin_remark
We don't really need $K$ to be a field, it could just be an associative
unital ring\footnote{\url{https://encyclopediaofmath.org/wiki/General_linear_group}}.
#+end_remark

This is a deceptive example, because it defines a "functor": given a
specific $n$ and $K$, we get a group. Most of this has been formalized
in, e.g., [[http://mizar.org/version/current/html/matrix_3.html][~matrix_3~]] and related articles ("invertible" is defined in
[[http://mizar.org/version/current/html/matrix_6.html#V1][~matrix_6~]]).

Matrix inversion is closed on the set of invertible square matrices, as
proven in theorem [[http://mizar.org/version/current/html/matrix_6.html#T16][~MATRIX_6:16~]].

Matrix multiplication is closed on the set of invertible square
matrices, as proven in [[http://mizar.org/version/current/html/matrix_6.html#T45][~MATRIX_6:45~]].

Associativity of matrix multiplication is proven in [[http://mizar.org/version/current/html/matrix_3.html#T33][~MATRIX_3:33~]].

#+begin_src mizar
definition
  let n, k, m be Nat;
  let K be Ring;
  let A be Matrix of n,k,K;
  let B be Matrix of width A,m,K;
  func matrix_mult (A,B) -> Matrix of len A, width B,K equals
  A*B;
end;

definition
  let n be Nat;
  let K be Field;
  func GL(n,K) -> Group means
  (for M being object
   holds (M is invertible Matrix of n,K) iff (M in it)) &
  (the multF of it = matrix_mult||(the carrier of it));
  existence
  proof end;
  uniqueness
  proof end;
end;

:: MATRIX_3:18 proves (1.(K,n))*A = A
:: MATRIX_3:19 proves A*(1.(K,n)) = A
theorem
  for n being Nat
  for K being Field
  holds 1_(GL(n,K)) = 1.(K,n) by GROUP_1:def 4,MATRIX_3:18,19;

theorem
  for n being Nat
  for K being Field
  for M being Element of GL(n,K)
  holds inverse_op(GL(n,K)).M = M ~ by MATRIX_6:def 4,GROUP_1:def 6;
#+end_src

** Special Linear Group
:PROPERTIES:
:CUSTOM_ID: h-ba74f292-740f-42d2-911a-8309a7f159bc
:END:

#+begin_definition
Let $n$ be a positive integer, $K$ be a field.
The {{{dfn(Special Linear Group)}}} $SL(n,K)$ consists of
invertible matrices of determinant one, and its binary operation is
matrix multiplication.
#+end_definition

The determinant of the identity matrix being unity is proven in theorem
[[http://mizar.org/version/current/html/matrix_7.html#T16][~MATRIX_7:16~]].

#+begin_src mizar
definition
  let n be Nat;
  let K be Field;
  func SL(n,K) -> Group means
  :Def:
  (for M being invertible Matrix of n,K
   holds (Det M = 1.K) iff (M in it)) &
  (the multF of it = matrix_mult||(the carrier of it));
  existence
  proof end;
  uniqueness
  proof end;
end;

theorem
  for n being Nat
  for K being Field
  holds SL(n,K) is Subgroup of GL(n,K)
proof
  let n be Nat;
  let K be Field;
  set UH = the carrier of SL(n,K);
  set UG = the carrier of GL(n,K);
  A1: UH c= UG;
  hence UH c= UG;
  A2: [:UH,UH:] c= [:UG,UG:] by A1,ZFMISC_1:96;
  the multF of SL(n,K) = matrix_mult||UH &
  the multF of GL(n,K) = matrix_mult||UG by GROUP_2:def 5;
  hence thesis by A2,FUNCT_1:51;
end;
#+end_src

** Projective Linear Groups
:PROPERTIES:
:CUSTOM_ID: h-e3b101ac-8d30-46c0-a473-3e2b68080eaa
:END:

We could then define the projective linear groups as:

#+begin_src mizar
definition
  let n be Nat;
  let K be Field;
  func PGL(n,K) -> Group equals GL(n,K)./.(center GL(n,K));
  coherence
  proof
    thus GL(n,K)./.(center GL(n,K)) is Group;
  end;
  func PSL(n,L) -> Group equals SL(n,K)./.(center SL(n,K))
  coherence
  proof
    thus SL(n,K)./.(center SL(n,K)) is Group;
  end;
end;
#+end_src

#+begin_remark
If we include ~NAT_6~ in our registrations, then we can register
~positive~ for ~Nat~. Be sure to include ~XCMPLX_0~ in the vocabulary.
#+end_remark

#+begin_example
registration
  cluster positive for Nat;
  existence
  proof
    take 1;
    thus thesis;
  end;
  cluster zero for natural number;
  existence
  proof
    take 0;
    thus thesis;
  end;
  cluster non zero for Nat; :: natural number;
  existence
  proof
    take 1;
    thus thesis;
  end;
end;
registration
  cluster non zero -> positive for Nat;
  coherence
  proof
    for x being Nat st x is non zero holds x is positive
    proof
      let x be Nat;
      assume A1: x is non zero;
      then x <> 0;
      then x > 0;
      hence x is positive;
    end;
    hence thesis;
  end;
end;
#+end_example