#+TITLE: Example Definitions Finite Groups - Mizar
#+AUTHOR: Alex Nelson
#+EMAIL: pqnelson@gmail.com
#+DATE: <2022-02-19T19:09:20-08:00>
#+LANGUAGE: en
#+OPTIONS: H:5
#+HTML_DOCTYPE: html5
#+INCLUDE: ../../../org-macros.org
#+HTML_LINK_UP: ./index.html
#+HTML_LINK_HOME: ../../../index.html
#+HTML_HEAD_EXTRA: <link rel="stylesheet" type="text/css" href="../../../css/stylesheet.css" />
# Created Saturday February 19, 2022 at  7:09PM

* Introduction
:PROPERTIES:
:CUSTOM_ID: h-3514a396-c340-41d5-97f5-7fe965d4519c
:END:

This is a collection of definitions I intend to formalize in Mizar. The
hard part of the formalizations is the proof of existence and uniqueness
(or other correctness conditions).

We provide the proof skeletons for most of the relevant definitions, but
do not provide proofs.

* Extra-special groups
:PROPERTIES:
:CUSTOM_ID: h-2f274d71-e96e-4479-8d58-cfc921adb1df
:END:

Rotman's /Introduction to the Theory of Groups/, 4th ed, defines on page
124:

#+begin_definition
Let $G$ be a finite $p$ -group. We call it {{{dfn(Extra-special)}}}
if $Z(G)$ is cyclic and $\Phi(G)=Z(G)=G'$.
#+end_definition

This defines an attribute (hinted by being of the form "We call it ~<adjective>~
if..."). Its formalization is straightforward.

#+begin_src mizar
definition
  let p be prime Nat;
  let IT be finite p-group Group;
  attr IT is extra-special means
  :Def:
  (center IT is cyclic) & (Phi(IT) = center IT) &
  (center IT = IT`);
end;
#+end_src

* G Has Characteristic p
:PROPERTIES:
:CUSTOM_ID: h-2e5a9e76-d90b-4603-ac16-2df78d4fc0a8
:END:

These definitions are taken from {{{arXiv(1906.07216)}}}.

** p-Local Subgroups
:PROPERTIES:
:CUSTOM_ID: h-b6e05185-d9e8-4006-84ea-8a672fa6c0cf
:END:

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group whose order is
divisible by $p$. A {{{dfn($p$-local subgroup)}}} is one that looks like
$N_{H}(P)$ where $P$ is a nontrivial $p$-subgroup of $G$.
#+end_definition

My intuition is that this definition introduces an attribute "$p$-local"
for subgroups.

#+begin_src mizar
definition
  let p be Nat;
  let G be finite Group such that p divides card G;
  let IT be Subgroup of G;
  attr IT is p-local means
  :Def:
  p is prime &
  ex P being p-group Subgroup of G
  st P is non trivial & the multMagma of IT = Normalizer P;
end;
#+end_src

Since subgroup equality is defined only for strict subgroups, we cannot
say ~IT = Normalizer P~ without making ~IT~ a strict Subgroup of G. (The
normalizer of a subgroup is defined to be a strict subgroup.) For this
reason, we needed to check ~the multMagma of IT = Normalizer P~.

Also, I am not sure if we /need/ ~p~ to be prime; it's just those are
the cases we care about.

This also requires the following registrations:

#+begin_src mizar
registration
  let G be Group;
  cluster trivial for Subgroup of G;
  existence
  proof
    take H = (1).G;
    thus thesis;
  end;
  let p be Nat;

  cluster p-group for Subgroup of G;
  existence
  proof
    take H = (1).G;
    A4: card H = 1 by GROUP_6:11
              .= p |^ 0 by NEWTON:4;
    hence thesis by GROUP_10:def 17;
  end;

  cluster trivial p-group for Subgroup of G;
  existence
  proof
    take H = (1).G;
    A4: card H = 1 by GROUP_6:11
              .= p |^ 0 by NEWTON:4;
    hence thesis by GROUP_10:def 17;
  end;
end;
#+end_src

** Subgroup has Characteristic p
:PROPERTIES:
:CUSTOM_ID: h-653b4943-4209-4ab8-b213-033e920fc4d5
:END:

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group, $L\leq G$ be a subgroup.
We say $L$ {{{dfn(has Characteristic $p$)}}} if
$C_{G}(O_{p}(L))\leq O_{p}(L)$.
#+end_definition

This introduces a new predicate, ~has_characteristic p~ for a subgroup.

#+begin_src mizar
definition
  let p be prime Nat;
  let G be Group;
  let L be Subgroup of G;
  pred L has_characteristic p means
  :Def:
  Centralizer (p-core L) is Subgroup of (p-core L);
end;
#+end_src

** Group has local characteristic p
:PROPERTIES:
:CUSTOM_ID: h-d5aaeccc-3c34-456c-973d-54ef0425ec81
:END:

#+begin_definition
Let $p$ be a prime number, $G$ be a finite group.
We say $G$ {{{dfn(has local Characteristic $p$)}}} if
if for all $p$-local subgroups $L$ of $G$ has characteristic $p$.
#+end_definition

(A special case when $p = 2$, we just say that $G$ has even
Characteristic.)

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  pred G has_local_characteristic p means
  :Def:
  for L being Subgroup of G st L is p-local holds L has_characteristic p;
end;
#+end_src

** Group has characteristic p
:PROPERTIES:
:CUSTOM_ID: h-328dc5eb-90cd-4c55-b09f-39464b5aca19
:END:

From page ix of [[https://users.math.msu.edu/users/meierfra/Preprints/CGP/Structure/structure.pdf][The Local Structure Theorem For Finite Groups With A Large p-Subgroup]]:

#+begin_definition
A finite group $G$ has {{{dfn(characteristic $p$)}}} if $C_{G}(O_{p}(G))\leq O_{p}(G)$.
#+end_definition

I would interpret this as a predicate (using the general heuristic "has ~<noun>~"
is a predicate).

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  pred G has_characteristic p means
  :Def:
  Centralizer (p-core G) is Subgroup of (p-core G);
end;

notation
  let G be finite Group;
  synonym G has_even_characteristic for G has_characteristic 2;
end;
#+end_src

* Parabolic Subgroup
:PROPERTIES:
:CUSTOM_ID: h-03074538-22a6-4bac-869b-a27c2bd4004c
:END:

#+begin_definition
Let $G$ be a finite group.
Any subgroup of $G$ containing a Sylow p-subgroup of $G$ is a
{{{dfn(parabolic subgroup)}}} of $G$.
#+end_definition

This defines an attribute "parabolic" for "Subgroup of $G$". We need to
use the Mizar predicate [[http://mizar.org/version/current/html/group_10.html#R3][~is_Sylow_p-subgroup_of_prime~]] to do the heavy lifting:

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  let IT be Subgroup of G;
  attr IT is parabolic means
  :Def:
  ex P being Subgroup of G st P is_Sylow_p-subgroup_of_prime p &
  P is Subgroup of IT;
end;
#+end_src

#+begin_danger
I believe this definition of "parabolic" *is nonstandard*. For finite
linear algebraic groups, this does not match the notion of parabolic
subgroups there. Instead the nonstandard definition is *analogous*,
using the analogy that Sylow p-subgroups "are like" maximal tori (since
they're conjugate to each other). A parabolic subgroup contains a
maximal torus.
#+end_danger


#+begin_definition
Let $p$ be a prime number, $G$ be a finite group.
We say $G$ has {{{dfn(parabolic characteristic $p$)}}} if
every $p$-local parabolic subgroup of $G$ has characteristic $p$.
#+end_definition

This defines a predicate (whenever a gadget "has ~<property>~", it
defines a predicate).

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  pred G has_parabolic_characteristic p means
  :Def:
  for H being parabolic Subgroup of G st H is p-local
  holds H has_characteristic p;
end;
#+end_src
* Finite Group Theory
:PROPERTIES:
:CUSTOM_ID: h-5ae83f58-ed1c-4543-8e5d-a49c61ba6701
:END:

** Sections of a Group
:PROPERTIES:
:CUSTOM_ID: h-86c68c5f-b76f-407c-aab0-aa4aecc6ceb9
:END:

Kurzweill and Stellmacher's /Theory of Finite Groups/ \S 1.3 concludes
with a brief definition in passing:

#+begin_definition
Let $G$ be a group, $N\trianglelefteq H\leq G$. We call the quotient
$H/N$ a {{{dfn(Section)}}} of $G$.
#+end_definition

In particular, $N$ is a normal subgroup of $H$ (viz., $N$ is not
necessarily a normal subgroup of $G$).

This appears to define a new mode (as hinted by "~<bork>~ of $G$").
*However* not all groups have a section (the quaternions famously do
not).

Consequently, we need to define this as a predicate.

#+begin_src mizar
definition
  let K,G be Group;
  pred K is_section_of G means :Def:
  ex H being Subgroup of G st
  ex N being normal Subgroup of H st
  K = H./.N;
end;
#+end_src

Note: we may need to change the denominator to ~(H,N)`*`~ which will
"cast" ~N~ as a normal subgroup of ~H~.

We also have a related notion "$G$ is involved in $K$" means $G$ is
isomorphic to a section of $K$. This is a transitive relation.

* Exponent of Group
:PROPERTIES:
:CUSTOM_ID: h-71ef82c3-d433-4eba-98a8-bac942140338
:END:

#+begin_definition
Let $G$ be a group. Its {{{dfn(Exponent)}}} is the least natural number
$n\in\mathbb{N}$ such that every $g\in G$ satisfies $g^{n}=1_{G}$. If
there is an element of order 0 in the group, then the exponent of $G$ is
defined to be 0.
#+end_definition

#+begin_src mizar
definition
  let G be Group;
  func exponent G -> Nat means
  :Def:
  it = 0 if ex g being Element of G st g is being_of_order_0
  otherwise it > 0 &
            for g being Element of G
            holds g |^ it = 1_G &
                  (for n being Nat st g |^ n = 1_G holds it <= n);

  existence
  proof
    thus (ex g being Element of G st g is being_of_order_0) implies
    (ex k being Nat st k = 0);
    thus (for g being Element of G holds not g is being_of_order_0) implies
    (ex k being Nat st k > 0 &
     for g being Element of G
     holds g |^ k = 1_G & (for n being Nat st g |^ n = 1_G holds k <= n));
  end;

  uniqueness
  proof
    defpred P[Element of Nat] means $1 > 0 &
    for g being Element of G
    holds g |^ ($1) = 1_G & (for n being Nat st g |^ n = 1_G holds ($1) <= n);

    let k1,k2 be Nat;

    thus (ex g being Element of G st g is being_of_order_0) & k1 = 0 & k2 = 0
    implies k1 = k2;

    thus (for g being Element of G holds not g is being_of_order_0) & P[k1] & P[k2]
    implies k1 = k2;
  end;

  consistency;
end;
#+end_src

* Elementary Abelian Group
:PROPERTIES:
:CUSTOM_ID: h-bea1584a-3260-4505-8938-578af0e8d5db
:END:

Aschbacher's {{{book-title(Finite Group Theory)}}} nicely defines an
elementary Abelian group right before \S1.13:

#+begin_definition
Let $p$ be a prime number, we define an {{{dfn(Elementary Abelian $p$ -Group)}}}
to be an Abelian $p$ -Group of exponent $p$.
#+end_definition

Luckily, ~groupp_1~ defines [[http://mizar.org/version/current/html/groupp_1.html#V3][~p-commutative-group~]] and which does a lot of
heavy lifting for us.

#+begin_src mizar
definition
  let p be prime Nat;
  let IT be finite Group;
  attr IT is p-elementary_Abelian means
  :Def:
  IT is p-commutative-group & exponent IT = p;
end;

:: Kurzweil & Stellmacher, _Theory of Finite Groups_, Thm 5.2.6
theorem
  for G being p-elementary_Abelian Group
  holds Phi(G) is trivial;

:: Kurzweil & Stellmacher, _Theory of Finite Groups_, Thm 5.2.7 (a)
theorem
  for P being p-group Group
  holds P./.Phi(P) is p-elementary_Abelian Group;
#+end_src

We will also want to define the $p$ rank of a finite group. This would
be the largest [[http://mizar.org/version/current/html/groupp_1.html#K1][~expon~]] of the ~p-elementary_Abelian~ subgroups of ~G~.

** p-Rank of a Group
:PROPERTIES:
:CUSTOM_ID: h-828bcfe4-8a74-441f-bf92-051a2c1c4d24
:END:

#+begin_definition
Let $G$ be a group, $p$ be a prime. The {{{dfn($p$ -rank)}}} of $G$ is
the maximum rank of all elementary Abelian $p$ -subgroups of $G$.
#+end_definition

This is only really well-defined if $G$ is a finite group whose order is
divisible by $p$, or if there exists an element whose order is divisible
by $p$. Since we only really care about the $p$ -rank for *finite*
groups, we can restrict our definition to finite groups.

#+begin_src mizar
definition
  let p be prime Nat;
  let G be finite Group;
  func p-rank G means
  :Def:
  it > 0 & for E being Subgroup of G
           st E is p-elementary_Abelian
           holds expon(E,p) <= it &
                 (for n being Nat
                  st n > 0 & expon(E,p) <= n
                  holds it <= n)
  if p divides card G
  otherwise it = 0;

  existence
  proof
    defpred P[Nat] means
    ($1) > 0 & for E being Subgroup of G
               st E is p-elementary_Abelian
               holds expon(E,p) <= ($1) &
                     (for n being Nat st n > 0 & expon(E,p) <= n
                      holds ($1) <= n);
    thus p divides card G implies ex k being Nat st P[k];
    thus not p divides card G implies ex k being Nat st k=0;
  end;

  uniqueness
  proof
    defpred P[Nat] means
    ($1) > 0 & for E being Subgroup of G
               st E is p-elementary_Abelian
               holds expon(E,p) <= ($1) &
                     (for n being Nat st n > 0 & expon(E,p) <= n
                      holds ($1) <= n);
    let k1,k2 be Nat;
    thus p divides card G & P[k1] & P[k2] holds k1 = k2;
    thus not (p divides card G) & (k1 = 0) & (k2 = 0) implies k1 = k2;
  end;

  consistency;
end;

notation
  let p be prime Nat, G be finite Group;
  synonym p-depth G for p-rank G;
end;
#+end_src

** Depth of Group
:PROPERTIES:
:CUSTOM_ID: h-577c3222-c227-4f29-a1ce-778af705a4b5
:END:

#+begin_definition
Let $G$ be a finite group.
The {{{dfn(Depth)}}} (or {{{dfn(Rank)}}}) of $G$ is the largest $p$
-rank of the group, for all primes $p$.
#+end_definition

We will run into a "name collision" in homological algebra, since "rank"
has a technical meaning there, and we need to use group cohomology later on.

#+begin_src mizar
definition
  let G be finite Group;
  func depth G -> Nat means
  for p being prime Nat
  holds p-rank G <= it & (for n being Nat
                          st p-rank G <= n
                          holds it <= n);

  existence
  proof
    thus ex k being Nat st
         for p being prime Nat
         holds p-rank G <= k &
               (for n being Nat st p-rank G <= n holds k <= n);
  end;

  uniqueness
  proof
    defpred P[Nat] means for p being prime Nat
    holds p-rank G <= ($1) & (for n being Nat
                              st p-rank G <= n
                              holds ($1) <= n);
    thus for k1,k2 being Nat st P[k1] & P[k2] holds k1=k2;
  end;
end;

theorem
  for G being trivial Group
  for p being prime Nat
  holds p-rank G = 0;
#+end_src

** Thin and Quasithin groups
:PROPERTIES:
:CUSTOM_ID: h-8f7b98d9-997c-4089-8641-56911841c828
:END:

There is a way to measure the "size" of a finite group,
$e(G)\in\mathbb{N}_{0}$, defined as the largest $p$ -rank (for odd
primes $p$) among all the 2-local subgroups of $G$.

- Aschbacher, in his "Groups of Characteristic 2-Type" refers to $e(G) = \max\{m_{2,p}(G) : p\mbox{ odd prime}\}$ as the
  {{{dfn(Rank)}}} of $G$.
- Gorenstein's article, "Classification theory of finite simple groups"
  (1980) refers to $e(G)$ as {{{dfn(odd 2-local rank)}}} of $G$. He also
  notes: "By a theorem of Frobenius, $G$ has a normal 2-complement if $e(G) = 0$, so $e(G)$ is always positive in a simple group."


#+begin_src mizar
definition
  let G be finite Group;
  func odd_2-local_rank G -> Nat means
  for p being odd prime Nat
  for M being Subgroup of G
  st M is 2-local
  holds p-rank M <= it &
        (for n being Nat
         st p-rank M <= n
         holds it <= n);

  existence
  proof
    defpred P[Nat] means for p being odd prime Nat
                         for M being Subgroup of G
                         st M is 2-local
                         holds p-rank M <= ($1) &
                               (for n being Nat
                                st p-rank M <= n
                                holds ($1) <= n);
    thus ex eG being Nat st P[eG];
  end;

  uniqueness
  proof
    defpred P[Nat] means for p being odd prime Nat
                         for M being Subgroup of G
                         st M is 2-local
                         holds p-rank M <= ($1) &
                               (for n being Nat
                                st p-rank M <= n
                                holds ($1) <= n);
    thus for k1,k2 being Nat st P[k1] & P[k2] holds k1=k2;
  end;
end;
#+end_src

This requires the following registration:

#+begin_src mizar
registration
  cluster odd for prime Nat;
  existence
  proof
    A1: 3 is prime by PEPIN:41;
    3 = 2*1+1;
    then 3 is odd by ABIAN:1;
    hence thesis by A1,PEPIN:17;
  end;
end;
#+end_src

We can now define thin and quasithin groups. They are defined using the
maximum $p$ -rank (over all odd prime $p$) for 2-local subgroups of $G$.

#+begin_src mizar
definition
  let IT be finite Group;
  attr IT is thin means
  :Def:
  odd_2-local_rank IT <= 1;

  attr IT is quasithin means
  :Def:
  odd_2-local_rank IT <= 2;

  :: Aschbacher & Smith, Classification of Quasithin Groups, I, pg 4
  attr IT is strongly_quasithin means
  :Def:
  for p being odd prime Nat
  holds p-rank IT <= 2;
end;

theorem the trivial Group is thin;
theorem the trivial Group is quasithin;

registration
  cluster thin finite Group
  existence
  proof
    take the trivial Group;
    hence thesis;
  end;

  cluster quasithin finite Group
  existence
  proof
    take the trivial Group;
    hence thesis;
  end;
end;

registration
  cluster thin -> quasithin finite Group;
  coherence
  proof
    thus for G being finite Group st G is thin holds G is quasithin;
  end;
end;
#+end_src

* Feit-Thompson Odd-Order Theorem
:PROPERTIES:
:CUSTOM_ID: h-90ed0478-53b3-42e6-9dd1-d3733258f9ca
:END:

The statement is quite easy:

#+begin_src mizar
theorem
  for G being finite Group
  st card G is odd
  holds G is solvable;
#+end_src

The proof has been revised and can be found spanning two books:

1. Helmut Bender and George Glauberman,\\
   {{{book-title(Local Analysis for the Odd Order Theorem)}}}.\\
   Cambridge University Press, 1995.
2. Thomas Peterfalvi,\\
   {{{book-title(Character Theory for the Odd Order Theorem)}}}.\\
   Cambridge University Press, 2000.

The main dependency seems to be the first 7 chapters of I. Martin
Isaacs's {{{book-title(Character Theory of Finite Groups)}}}, which
Dover has reprinted in 1994.

* Group Extensions
:PROPERTIES:
:CUSTOM_ID: h-a6ee7bdb-62b4-45ab-bfe2-f7d29674a3b3
:END:

Group extensions are ambiguously used in the literature. Sometimes it
refers to a short exact sequence $1\to A\to E\to G\to 1$, other times it
refers to the group $E$, other times it is the surjective morphism $E\to G$
from the short exact sequence. Bourbaki defines a group extension in
definition 1 ({{{book-title(Algebra)}}} ch. I \S6.1) as:

#+begin_definition
Let $F$ and $G$ be two groups.
A {{{dfn(Extension)}}} of $G$ by $F$ is a triple
$\mathcal{E} = (E,i\colon F\hookrightarrow E, p\colon E\to G)$ where $E$
is a group, $i\colon F\hookrightarrow E$ is an injective group morphism,
and $p\colon E\to G$ is a surjective group morphism such that
$\mathrm{Im}(i)=\mathrm{Ker}(p)$. 
#+end_definition

This seems like something which should be considered *after* formalizing
Homological Algebra (or during its formalization)...

#+begin_src mizar
theorem Th1:
  for G being Group
  for H being Subgroup of G
  ex f being Homomorphism of H,G
  st (for h being Element of H holds f.h = h) & f is one-to-one &
  Image f = the multMagma of H;
::>                          *4

definition
  let G be Group;
  mode Extension of G -> Group means :Def:
  ex A being Group st
  ex f being Homomorphism of A,it st
  ex p being Homomorphism of it,G
  st Image f is Subgroup of Ker p &
  f is one-to-one & p is onto;

  existence
  proof
    thus ex E being Group st
    ex A being Group st
    ex f being Homomorphism of A,E st
    ex p being Homomorphism of E,G
    st Image f is Subgroup of Ker p &
    f is one-to-one & p is onto
    proof
      take E=G;
      take A=(1).G;
      consider f being Homomorphism of A,E such that
      A1: for g being Element of A holds f.g = g and
      A2: f is one-to-one and
      A3: Image f = the multMagma of A
      by Th1;
      take f;
      reconsider p=id G as Homomorphism of E,G by GROUP_6:38;
      id the carrier of G is onto;
      then A4: p is onto;
      (1).(Ker p) = (1).G by GROUP_2:63;
      then (1).G is Subgroup of Ker p;
      then Image f is Subgroup of Ker p by A3;
      hence thesis by A2,A3;
    end;
  end;
end;

definition
  let G be Group;
  mode CentralExtension of G -> Group means :Def:
  ex A being Group st
  ex f being Homomorphism of A,it st
  ex p being Homomorphism of it,G
  st Image f is Subgroup of Ker p & Image f is Subgroup of center it &
  f is one-to-one & p is onto;

  existence
  proof
    thus ex E being Group st
    ex A being Group st
    ex f being Homomorphism of A,E st
    ex p being Homomorphism of E,G
    st Image f is Subgroup of Ker p &
    Image f is Subgroup of center E &
    f is one-to-one & p is onto
    proof
      take E=G;
      take A=(1).G;
      consider f being Homomorphism of A,E such that
      A1: for g being Element of A holds f.g = g and
      A2: f is one-to-one and
      A3: Image f = the multMagma of A
      by Th1;
      take f;
      reconsider p=id G as Homomorphism of E,G by GROUP_6:38;
      id the carrier of G is onto;
      then A4: p is onto;
      (1).(Ker p) = (1).G by GROUP_2:63;
      then (1).G is Subgroup of Ker p;
      then A5: Image f is Subgroup of Ker p by A3;
      (1).(center E) = (1).G by GROUP_2:63;
      then (1).G is Subgroup of center E;
      then Image f is Subgroup of center E by A3;
      hence thesis by A2,A3,A5;
    end;
  end;
end;
#+end_src